#? stdtmpl() | standard
#
#import std/[options, sets]
#
#import ../../../api
#import ../../../helpers
#
#proc generate*(
#    def: ClassDefinition): string =
#  let selfModule = def.moduleName().safeImport()
#  let selfModuleExport = def.moduleName().safeIdent()
#  let selfType = def.name
#
#  result = autogenDisclaimer()
#  result &= "# Builtin Class " & def.name & " (Procs)\n"
import ./types/$selfModule
export $selfModuleExport
#  if def.inherits.isSome():
#    let parentName = def.inherits.unsafeGet()
#    let parentModuleName = parentName.moduleName().safeImport()
#    let parentModuleNameExport = parentName.moduleName().safeIdent()

import ../classes/$parentModuleName
export $parentModuleNameExport
#  end if
#  if def.name == "Thread":

type Thread = thread.Thread
#  end if

#  result &= def.renderImportList(
#    {roProperties, roMethods, roStructFields, roConstructors, roIndexes, roOperators},
#     "nodot/classes", def.inherits)
#
import internal/instance_funcs

#  if def.needsVTable:
#    let vtable = def.renderVTable()
$vtable
#
#  # All pointers start out as "nil", as no virtual method has any defined
#  # behaviour from Godot unless we override it.
let vtable: ${def.name}VTable = default(${def.name}VTable)
#  end if

#  if def.needsVTable:
proc gdVTablePointer*(_: typedesc[$def.name]): ptr ${def.name}VTable =
#  else:
proc gdVTablePointer*(_: typedesc[$def.name]): ptr $def.parentVTable =
#  end if
#  if def.needsVTable:
  unsafeAddr vtable
#  elif def.inherits.isSome():
#    let parent = def.inherits.unsafeGet()
  ${parent}.gdVTablePointer()
#  else:
  nil
#  end if

let instanceBindingCallbacks* = makeInstanceFunctions($def.name)

proc gdInstanceBindingCallbacks*(_: typedesc[$def.name]): ptr GDExtensionInstanceBindingCallbacks =
  unsafeAddr instanceBindingCallbacks

# #instanceBindingCallbacks = makeInstanceFunctions($def.name)

#  if def.constants.isSome:
#    result &= "# Constants\n"
#
#    for constant in def.constants.unsafeGet():
#      let constName = constant.name.safeIdent()
#      let constVal = constant.value
#
const $constName*: int = $constVal
#    end for

#  end if
#
#  let ctorName = def.name.deriveCtorName()
#
#  if def.is_instantiable and not def.is_singleton:
#    result &= "# Constructor\n"
#
#  if def.is_refcounted and def.is_instantiable:
proc $ctorName*(): Ref[$selfType]
#  elif def.is_instantiable:
proc $ctorName*(): $selfType
#  end if
  {.gd_class_ctor.}

#  elif def.is_singleton:
proc $ctorName*(): $selfType
  {.gd_class_singleton.}

#  end if
#  if def.methods.isSome:
#    result &= "# Methods\n"
#
#    for meth in def.methods.unsafeGet():
#      let methPrototype = meth.render(def)
#
#      if meth.is_virtual:
#        let vtableEntry = meth.vtableEntryName()
$methPrototype
  {.gd_class_method_virtual("$meth.name", ${def.name}VTable.$vtableEntry).}
#      else:
$methPrototype
#        let hash = meth.hash.unsafeGet()
#
#        if meth.return_value.isSome() and meth.return_value.unsafeGet().`type`.isClassType:
  {.gd_class_method_obj($hash).}
#        else:
  {.gd_class_method($hash).}
#        end if
#      end if

#    end for
#  end if
#
#  # Properties seem infeasible to auto generate for now.
#  if false and def.properties.isSome:
#    result &= "# Properties\n"
#
#    for property in def.properties.unsafeGet():
#      if def.definesMethod(property.getter):
#        let getterProto = def.renderGetter(property)
$getterProto = discard
#      end if
#      if property.setter.isSome():
#        if def.definesMethod(property.setter.unsafeGet()):
#          let setterProto = def.renderSetter(property)
$setterProto = discard
#        end if
#      end if
#    end for
#  end if